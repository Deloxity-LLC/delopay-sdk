plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

group = project.findProperty("GROUP")
version = project.findProperty("VERSION_NAME")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind:2.21.0"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.21.0"

    testImplementation "org.junit.jupiter:junit-jupiter:6.0.3"
    testImplementation "com.squareup.okhttp3:mockwebserver:5.3.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:6.0.2"
}

test {
    useJUnitPlatform()
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption("Xdoclint:none", "-quiet")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "io.delopay"
            artifactId = "delopay-java"
            version = project.version

            from components.java

            pom {
                name = "delopay-java"
                description = "Official DeloPay Java SDK"
                url = "https://github.com/Deloxity-LLC/delopay-sdk"
                licenses {
                    license {
                        name = "Proprietary"
                    }
                }
                developers {
                    developer {
                        id = "delopay"
                        name = "DeloPay"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/Deloxity-LLC/delopay-sdk.git"
                    developerConnection = "scm:git:ssh://git@github.com/Deloxity-LLC/delopay-sdk.git"
                    url = "https://github.com/Deloxity-LLC/delopay-sdk"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            def snapshotsUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            def releasesUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            url = version.toString().endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

            credentials {
                username = System.getenv("MAVEN_CENTRAL_USERNAME")
                password = System.getenv("MAVEN_CENTRAL_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = System.getenv("MAVEN_GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("MAVEN_GPG_PASSPHRASE")

    required = signingKey != null && signingPassword != null

    if (signingKey != null && signingPassword != null) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}
